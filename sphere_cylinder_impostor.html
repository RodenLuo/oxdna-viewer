<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<title>My first three.js app</title>
		<style>
			body { margin: 0; }
			canvas { display: block; }
		</style>
	</head>
	<body>
        <script src="./ts/lib/CCapture.all.min.js"></script>
        <script src="./ts/lib/three.js"></script>
        <script src="./OrbitControls.r88.js"></script>

        <script id="shaderCyl-vs" type="x-shader/x-vertex">
        // Inherits from Phong Shading in ThreeJS, allows compatibility with chunks
        #define PHONG
        
        // Normalize diagonal matrix (1)
        const mat4 D = mat4(
            vec4(1.0, 0.0, 0.0, 0.0),
            vec4(0.0, 1.0, 0.0, 0.0),
            vec4(0.0, 0.0, 0.0, 0.0),
            vec4(0.0, 0.0, 0.0, -1.0)
        );
        
        const mat4 t1 = mat4(
            vec4(1.0, 0.0, 0.0, 0.0),
            vec4(0.0, 1.0, 0.0, 0.0),
            vec4(0.0, 0.0, 0.0, 0.0),
            vec4(0.0, 0.0, 1.0, 1.0)
        );
        
        const mat4 t2 = mat4(
            vec4(1.0, 0.0, 0.0, 0.0),
            vec4(0.0, 1.0, 0.0, 0.0),
            vec4(0.0, 0.0, 0.0, 0.0),
            vec4(0.0, 0.0, -1.0, 1.0)
        );
        
        // Create transpose method (not built-in OpenGL ES Shading Language)
        highp mat4 transpose(in highp mat4 inMatrix) {
            highp vec4 i0 = inMatrix[0];
            highp vec4 i1 = inMatrix[1];
            highp vec4 i2 = inMatrix[2];
            highp vec4 i3 = inMatrix[3];
        
            highp mat4 outMatrix = mat4(
                vec4(i0.x, i1.x, i2.x, i3.x),
                vec4(i0.y, i1.y, i2.y, i3.y),
                vec4(i0.z, i1.z, i2.z, i3.z),
                vec4(i0.w, i1.w, i2.w, i3.w)
            );
        
            return outMatrix;
        }
        
        //#include <common>
        #include <color_pars_vertex>
        #include <logdepthbuf_pars_vertex>
        #include <clipping_planes_pars_vertex>
        
        uniform float radius;
        attribute vec3 dir;
        
        uniform vec4 viewport;
        uniform mat4 modelViewMatrixInverse;
        
        varying mat4 MTInverse;
        
        
        
        // Near, far, width and height
        varying float n, f, w, h;
        varying float projMatrix11;
        varying float projMatrix22;
        
        // Compatibility with threejs chunks
        varying vec3 vViewPosition;
        varying vec3 vViewDirection;
        varying float vLength;
        
        void main() {
            // Chunck to color vertex with vertex color attribute
            #include <color_vertex>
        
            // Chunck to compute position in world space
            #include <begin_vertex>
            #include <project_vertex>
            #include <logdepthbuf_vertex>
            #include <clipping_planes_vertex>
        
            vViewPosition = - mvPosition.xyz;
        
            float zScale = length(dir);
            vec3 normDir = dir / zScale;
        
            // Computing T matrix with u,v,w and c as quadratic surface parameters (2)
            mat4 Tscalet = mat4(
                vec4(radius,        0.0,           0.0,           0.0),
                vec4(0.0,           radius,        0.0,           0.0),
                vec4(0.0,           0.0,           zScale,        0.0),
                vec4(0.0,           0.0,           0.0,           1.0)
            );
                
            vec3 up;
            bool xMain = abs(normDir[0]) > 0.9;
            bool yMain = abs(normDir[1]) > 0.9;
            bool zMain = abs(normDir[2]) > 0.9;
            
            if (yMain) {
                up = vec3( 1, 0, 0 );
            } else {
                up = vec3( 0, 1, 0 );
            }
            vec3 xAxis = normalize(cross(up, normDir.xyz));
            vec3 yAxis = normalize(cross(normDir.xyz, xAxis));
            mat4 Trott = mat4(
                vec4(xAxis.xyz,     0.0),
                vec4(yAxis.xyz,     0.0),
                vec4(normDir.xyz,   0.0),
                vec4(0.0, 0.0, 0.0, 1.0)
            );
        
            mat4 Ttranst = mat4(
                vec4(1.0, 0.0, 0.0, 0.0),
                vec4(0.0, 1.0, 0.0, 0.0),
                vec4(0.0, 0.0, 1.0, 0.0),
                vec4(position.x,    position.y,    position.z,    1.0)
            );
        
            mat4 Tt = Ttranst * Trott * Tscalet;
            
            vViewDirection = vec3(modelViewMatrix * (Trott * (vec4(0.0, 0.0, 1.0, 0.0))));
            vLength = zScale;
        
            // Computing PMTt Matrix
            mat4 PMTt = transpose(projectionMatrix * modelViewMatrix * Tt * t1);
        
            // Getting coefficients
            vec4 r1 = PMTt[0];
            vec4 r2 = PMTt[1];
            vec4 r4 = PMTt[3];
        
            // Intermediate computations
            float r4TDr4 = dot(r4, D * r4);
            float r1TDr4 = dot(r1, D * r4);
            float r1TDr1 = dot(r1, D * r1);
            float r2TDr4 = dot(r2, D * r4);
            float r2TDr2 = dot(r2, D * r2);
        
            // Computing bounding box by resolving second degree equations (5)
            vec2 vertBorders1;
            vec2 horizBorders1;
            float A, B, C;
            A = r4TDr4; B = -2.0 * r1TDr4; C = r1TDr1;
            horizBorders1[0] = ( -B - sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            horizBorders1[1] = ( -B + sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            A = r4TDr4; B = - 2.0 * r2TDr4; C = r2TDr2;
            vertBorders1[0] = ( -B - sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            vertBorders1[1] = ( -B + sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
        
                // Computing PMTt Matrix
            PMTt = transpose(projectionMatrix * modelViewMatrix * Tt * t2);
        
            // Getting coefficients
            r1 = PMTt[0];
            r2 = PMTt[1];
            r4 = PMTt[3];
        
            // Intermediate computations
            r4TDr4 = dot(r4, D * r4);
            r1TDr4 = dot(r1, D * r4);
            r1TDr1 = dot(r1, D * r1);
            r2TDr4 = dot(r2, D * r4);
            r2TDr2 = dot(r2, D * r2);
        
            // Computing bounding box by resolving second degree equations (5)
            vec2 vertBorders2;
            vec2 horizBorders2;
            A = r4TDr4; B = -2.0 * r1TDr4; C = r1TDr1;
            horizBorders2[0] = ( -B - sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            horizBorders2[1] = ( -B + sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            A = r4TDr4; B = - 2.0 * r2TDr4; C = r2TDr2;
            vertBorders2[0] = ( -B - sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            vertBorders2[1] = ( -B + sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            
            vec2 vertBorders;
            vec2 horizBorders;
            horizBorders[ 0 ] = min( horizBorders1[ 0 ], min( horizBorders1[ 1 ], min( horizBorders2[ 0 ], horizBorders2[ 1 ] ) ) );
                horizBorders[ 1 ] = max( horizBorders1[ 0 ], max( horizBorders1[ 1 ], max( horizBorders2[ 0 ], horizBorders2[ 1 ] ) ) );
        
                vertBorders[ 0 ] = min( vertBorders1[ 0 ], min( vertBorders1[ 1 ], min( vertBorders2[ 0 ], vertBorders2[ 1 ] ) ) );
                vertBorders[ 1 ] = max( vertBorders1[ 0 ], max( vertBorders1[ 1 ], max( vertBorders2[ 0 ], vertBorders2[ 1 ] ) ) );
            
            // Getting bounding box size in window space
            float bx = abs( horizBorders[ 0 ] - horizBorders[ 1 ] ) * 0.5 * viewport.z;
            float by = abs( vertBorders[ 0 ] - vertBorders[ 1 ] ) * 0.5 * viewport.w;
        
            // Getting bounding box center in clip space
            float cx = 0.5 * (horizBorders.x + horizBorders.y);
            float cy = 0.5 * (vertBorders.x + vertBorders.y);
        
            // Setting point size
            gl_PointSize = ceil(max(bx, by));
        
            // Reposition vertex to fit with bounding box
            //gl_Position.xy = vec2(cx, cy) * gl_Position.w;
        
            // Prepare varyings for fragment shader
            mat4 TscaleInv = mat4(
                vec4(1.0 / radius,        0.0,           0.0,           0.0),
                vec4(0.0,           1.0 / radius,        0.0,           0.0),
                vec4(0.0,           0.0,           1.0 / zScale,        0.0),
                vec4(0.0,           0.0,           0.0,           1.0)
            );
            
            mat4 TrotInv = transpose(Trott);
        
            mat4 TtransInv = mat4(
                vec4(1.0, 0.0, 0.0, 0.0),
                vec4(0.0, 1.0, 0.0, 0.0),
                vec4(0.0, 0.0, 1.0, 0.0),
                vec4(-position, 1.0)
            );
            
            mat4 TInverse = TscaleInv * TrotInv * TtransInv;
            
            MTInverse = TInverse * modelViewMatrixInverse;
        
            // Getting near and far
            n = projectionMatrix[3][2] / (projectionMatrix[2][2] - 1.0);
            f = ((projectionMatrix[2][2] - 1.0) * n) / (projectionMatrix[2][2] + 1.0);
        
            // Getting width and height of the viewport for fragment
            w = viewport.z;
            h = viewport.w,
        
            // Getting projection matrix diagonal
            projMatrix11 = projectionMatrix[0][0];
            projMatrix22 = projectionMatrix[1][1];
        }
        </script>
        <script id="shaderCyl-fs" type="x-shader/x-fragment">
        // Inherits from Phong Shading in ThreeJS, allows compatibility with chunks
        #define PHONG
        
        #include <common>
        #include <fog_pars_fragment>
        #include <color_pars_fragment>
        #include <lights_pars_begin>
        #include <bsdfs>
        #include <lights_phong_pars_fragment>
        #include <clipping_planes_pars_fragment>
        
        // Normalize diagonal matrix (1)
        const mat4 D = mat4(
            vec4(1.0, 0.0, 0.0, 0.0),
            vec4(0.0, 1.0, 0.0, 0.0),
            vec4(0.0, 0.0, 0.0, 0.0),
            vec4(0.0, 0.0, 0.0, -1.0)
        );
        
        // Create transpose method (not built-in OpenGL ES Shading Language)
        highp mat4 transpose(in highp mat4 inMatrix) {
            highp vec4 i0 = inMatrix[0];
            highp vec4 i1 = inMatrix[1];
            highp vec4 i2 = inMatrix[2];
            highp vec4 i3 = inMatrix[3];
        
            highp mat4 outMatrix = mat4(
                vec4(i0.x, i1.x, i2.x, i3.x),
                vec4(i0.y, i1.y, i2.y, i3.y),
                vec4(i0.z, i1.z, i2.z, i3.z),
                vec4(i0.w, i1.w, i2.w, i3.w)
            );
        
            return outMatrix;
        }
        
        varying mat4 MTInverse;
        
        
        
        // Near, far, width and height
        varying float n, f, w, h;
        varying float projMatrix11;
        varying float projMatrix22;
        
        uniform vec3 diffuse;
        uniform vec3 emissive;
        uniform vec3 specular;
        uniform float shininess;
        uniform float opacity;
        uniform mat4 projectionMatrixInverse;
        
        varying vec3 vViewDirection;
        varying float vLength;
        
        void main() {
            gl_FragColor = vec4(1.0);
            float c = (f - n) / (2.0 * n * f) * 2.0 / (gl_DepthRange.diff);
            vec4 c3 = MTInverse[3] * c;
        
            // Computing xprime (6)
            vec4 xp = vec4(
                1.0 / projMatrix11 * (gl_FragCoord.x * 2.0 / w - 1.0),
                1.0 / projMatrix22 * (gl_FragCoord.y * 2.0 / h - 1.0),
                -1.0,
                (f + n) / (2.0 * f * n) - (n - f) /
                    (2.0 * f * n) * (gl_DepthRange.near + gl_DepthRange.far) / -gl_DepthRange.diff
            );
            vec4 xpPrime = MTInverse * xp;
        
            // Checking quadratic equation discriminant (7)
            float c3TDc3 = dot(c3, D * c3);
            float xpPrimeTDc3 = dot(xpPrime, D * c3);
            float xpPrimeTDxpPrime = dot(xpPrime, D * xpPrime);
            float A = c3TDc3, B = 2.0 * xpPrimeTDc3, C = xpPrimeTDxpPrime;
            float discriminant = B * B - 4.0 * A * C;
        
            gl_FragColor = vec4(1.0);
            if (discriminant < 0.0) {
                discard;
            } else {
                // Resolving solutions
                float x1 = (-B - sqrt(discriminant)) / (2.0 * A);
                float x2 = (-B + sqrt(discriminant)) / (2.0 * A);
                float z = 1.0 - max(x1, x2);
                gl_FragDepthEXT = z;
        
                // Getting current pixel clip coordinate
                vec4 vClipPos = vec4(
                    (gl_FragCoord.x * 2.0 - 1.0),
                    (gl_FragCoord.y * 2.0 - 1.0),
                    z * 2.0 - 1.0,
                    1.0
                );
                
                vec3 fc = vec3(gl_FragCoord.xy, z);
                fc.xy /= vec2(w, h);
                fc *= 2.0;
                fc -= 1.0;
                vec4 p = projectionMatrixInverse * vec4(fc, 1.0);
                p /= p.w;
                vec3 rayDir = normalize(p.xyz);
                
                float vViewNorm = dot(vViewDirection, vViewDirection);
                vec3 pDir = p.xyz + vViewPosition.xyz;
                
                float a = dot(pDir,vViewDirection) / vViewNorm;
                if (a < 0.0) {
                    discard;
                }
                vec3 newPoint = pDir.xyz + vViewDirection * a;
                
                
                if (length(newPoint) / 2.0625 > vLength) {
                    discard;
                }
                
                // Performing clipping
                #if NUM_CLIPPING_PLANES > 0
                for ( int i = 0; i < NUM_CLIPPING_PLANES; ++ i ) {
                    vec4 plane = clippingPlanes[ i ];
                    if ( dot(vViewPos.xyz, plane.xyz ) > plane.w ) {
                        discard;
                    }
                }
                #endif
        
                // Position in eye coordinates (8)
                vec4 pe = vec4(
                    1.0 / projMatrix11 * (gl_FragCoord.x * 2.0 / w - 1.0),
                    1.0 / projMatrix22 * (gl_FragCoord.y * 2.0 / h - 1.0),
                    -1.0,
                    (f + n)/(2.0 * f * n) -
                        (n - f)/(2.0 * f * n) * (gl_DepthRange.near + gl_DepthRange.far) / gl_DepthRange.diff +
                        z * ((n - f) / (n * f) / gl_DepthRange.diff)
                );
                vec4 np = MTInverse * pe;
                vec4 ne = transpose(MTInverse) * np;
        
                // Declaring normal to get compatibility with threejs Phong chunks
                vec3 normal = normalize(ne.xyz);
        
                // Starting to compute phong lighting
                vec4 diffuseColor = vec4(diffuse, opacity);
                ReflectedLight reflectedLight = ReflectedLight(vec3(0.0), vec3(0.0), vec3(0.0), vec3(0.0));
                vec3 totalEmissiveLight = emissive;
                #include <color_fragment>
                // Declaring specularStength to get compatibility with threejs chunks
                float specularStrength = 1.0;
                #include <lights_phong_fragment>
                #include <lights_fragment_begin>
                vec3 outgoingLight = reflectedLight.directDiffuse + reflectedLight.indirectDiffuse +
                                        reflectedLight.directSpecular + reflectedLight.indirectSpecular + totalEmissiveLight;
        
                gl_FragColor = vec4(outgoingLight, opacity);
        
                #include <fog_fragment>
            }
        }
        </script>
        <script id="shaderSphere-vs" type="x-shader/x-vertex">
        // Inherits from Phong Shading in ThreeJS, allows compatibility with chunks
        #define PHONG
        
        // Normalize diagonal matrix (1)
        const mat4 D = mat4(
            vec4(1.0, 0.0, 0.0, 0.0),
            vec4(0.0, 1.0, 0.0, 0.0),
            vec4(0.0, 0.0, 1.0, 0.0),
            vec4(0.0, 0.0, 0.0, -1.0)
        );
        
        // Create transpose method (not built-in OpenGL ES Shading Language)
        highp mat4 transpose(in highp mat4 inMatrix) {
            highp vec4 i0 = inMatrix[0];
            highp vec4 i1 = inMatrix[1];
            highp vec4 i2 = inMatrix[2];
            highp vec4 i3 = inMatrix[3];
        
            highp mat4 outMatrix = mat4(
                vec4(i0.x, i1.x, i2.x, i3.x),
                vec4(i0.y, i1.y, i2.y, i3.y),
                vec4(i0.z, i1.z, i2.z, i3.z),
                vec4(i0.w, i1.w, i2.w, i3.w)
            );
        
            return outMatrix;
        }
        
        //#include <common>
        #include <color_pars_vertex>
        //#include <logdepthbuf_pars_vertex>
        //#include <clipping_planes_pars_vertex>
        
        uniform float radius;
        
        uniform vec4 viewport;
        uniform mat4 modelViewMatrixInverse;
        
        varying mat4 MTInverse;
        
        
        
        // Near, far, width and height
        varying float n, f, w, h;
        varying float projMatrix11;
        varying float projMatrix22;
        
        // Compatibility with threejs chunks
        varying vec3 vViewPosition;
        
        void main() {
            // Chunck to color vertex with vertex color attribute
            #include <color_vertex>
        
            // Chunck to compute position in world space
            #include <begin_vertex>
            #include <project_vertex>
            //#include <logdepthbuf_vertex>
            #include <clipping_planes_vertex>
        
            vViewPosition = - mvPosition.xyz;
        
            // Computing T matrix with u,v,w and c as quadratic surface parameters (2)
            mat4 T = mat4(
                // Three following vectors represents u, v, w axis of the quadric
                vec4(radius,        0.0,           0.0,           0.0),
                vec4(0.0,           radius,        0.0,           0.0),
                vec4(0.0,           0.0,           radius,        0.0),
                // Represents c, center of the quadric
                vec4(position.x,    position.y,    position.z,    1.0)
            );
        
            // Computing PMTt Matrix
            mat4 PMTt = transpose(projectionMatrix * modelViewMatrix * T);
        
            // Getting coefficients
            vec4 r1 = PMTt[0];
            vec4 r2 = PMTt[1];
            vec4 r4 = PMTt[3];
        
            // Intermediate computations
            float r4TDr4 = dot(r4, D * r4);
            float r1TDr4 = dot(r1, D * r4);
            float r1TDr1 = dot(r1, D * r1);
            float r2TDr4 = dot(r2, D * r4);
            float r2TDr2 = dot(r2, D * r2);
        
            // Computing bounding box by resolving second degree equations (5)
            vec2 vertBorders;
            vec2 horizBorders;
            float A, B, C;
            A = r4TDr4; B = -2.0 * r1TDr4; C = r1TDr1;
            horizBorders[0] = ( -B - sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            horizBorders[1] = ( -B + sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            A = r4TDr4; B = - 2.0 * r2TDr4; C = r2TDr2;
            vertBorders[0] = ( -B - sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
            vertBorders[1] = ( -B + sqrt( B * B - 4.0 * A * C ) ) / ( 2.0 * A );
        
            // Getting bounding box size in window space
            float bx = abs( horizBorders[ 0 ] - horizBorders[ 1 ] ) * viewport.z;
            float by = abs( vertBorders[ 0 ] - vertBorders[ 1 ] ) * viewport.w;
        
            // Getting bounding box center in clip space
            float cx = 0.5 * (horizBorders.x + horizBorders.y);
            float cy = 0.5 * (vertBorders.x + vertBorders.y);
        
            // Setting point size
            gl_PointSize = max(bx, by) * 0.5;
        
            // Reposition vertex to fit with bounding box
            gl_Position.xy = vec2(cx, cy) * gl_Position.w;
        
            // Prepare varyings for fragment shader
            mat4 TInverse = mat4(
                1.0,          0.0,          0.0,         0.0,
                0.0,          1.0,          0.0,         0.0,
                0.0,          0.0,          1.0,         0.0,
                -position.x,  -position.y,  -position.z, radius
            );
            MTInverse = TInverse * modelViewMatrixInverse;
        
            // Getting near and far
            n = projectionMatrix[3][2] / (projectionMatrix[2][2] - 1.0);
            f = ((projectionMatrix[2][2] - 1.0) * n) / (projectionMatrix[2][2] + 1.0);
        
            // Getting width and height of the viewport for fragment
            w = viewport.z;
            h = viewport.w,
        
            // Getting projection matrix diagonal
            projMatrix11 = projectionMatrix[0][0];
            projMatrix22 = projectionMatrix[1][1];
        }
        </script>
        <script id="shaderSphere-fs" type="x-shader/x-fragment">
        // Inherits from Phong Shading in ThreeJS, allows compatibility with chunks
        #define PHONG
        
        #include <common>
        //#include <fog_pars_fragment>
        #include <color_pars_fragment>
        #include <lights_pars_begin>
        #include <bsdfs>
        #include <lights_phong_pars_fragment>
        //#include <clipping_planes_pars_fragment>
        
        // Normalize diagonal matrix (1)
        const mat4 D = mat4(
            vec4(1.0, 0.0, 0.0, 0.0),
            vec4(0.0, 1.0, 0.0, 0.0),
            vec4(0.0, 0.0, 1.0, 0.0),
            vec4(0.0, 0.0, 0.0, -1.0)
        );
        
        // Create transpose method (not built-in OpenGL ES Shading Language)
        highp mat4 transpose(in highp mat4 inMatrix) {
            highp vec4 i0 = inMatrix[0];
            highp vec4 i1 = inMatrix[1];
            highp vec4 i2 = inMatrix[2];
            highp vec4 i3 = inMatrix[3];
        
            highp mat4 outMatrix = mat4(
                vec4(i0.x, i1.x, i2.x, i3.x),
                vec4(i0.y, i1.y, i2.y, i3.y),
                vec4(i0.z, i1.z, i2.z, i3.z),
                vec4(i0.w, i1.w, i2.w, i3.w)
            );
        
            return outMatrix;
        }
        
        varying mat4 MTInverse;
        
        
        
        // Near, far, width and height
        varying float n, f, w, h;
        varying float projMatrix11;
        varying float projMatrix22;
        
        uniform vec3 diffuse;
        uniform vec3 emissive;
        uniform vec3 specular;
        uniform float shininess;
        uniform float opacity;
        uniform mat4 projectionMatrixInverse;
        
        void main() {
            float c = (f - n) / (2.0 * n * f) * 2.0 / (gl_DepthRange.diff);
            vec4 c3 = MTInverse[3] * c;
        
            // Computing xprime (6)
            vec4 xp = vec4(
                1.0 / projMatrix11 * (gl_FragCoord.x * 2.0 / w - 1.0),
                1.0 / projMatrix22 * (gl_FragCoord.y * 2.0 / h - 1.0),
                -1.0,
                (f + n) / (2.0 * f * n) - (n - f) /
                    (2.0 * f * n) * (gl_DepthRange.near + gl_DepthRange.far) / -gl_DepthRange.diff
            );
            vec4 xpPrime = MTInverse * xp;
        
            // Checking quadratic equation discriminant (7)
            float c3TDc3 = dot(c3, D * c3);
            float xpPrimeTDc3 = dot(xpPrime, D * c3);
            float xpPrimeTDxpPrime = dot(xpPrime, D * xpPrime);
            float A = c3TDc3, B = 2.0 * xpPrimeTDc3, C = xpPrimeTDxpPrime;
            float discriminant = B * B - 4.0 * A * C;
        
            gl_FragColor = vec4(1.0);
            if (discriminant < 0.0) {
                discard;
            } else {
                // Resolving solutions
                float x1 = (-B - sqrt(discriminant)) / (2.0 * A);
                float x2 = (-B + sqrt(discriminant)) / (2.0 * A);
                float z = 1.0 - max(x1, x2);
                gl_FragDepthEXT = z;
        
                // Getting current pixel clip coordinate
                vec4 vClipPos = vec4(
                    (gl_FragCoord.x * 2.0 - 1.0),
                    (gl_FragCoord.y * 2.0 - 1.0),
                    z * 2.0 - 1.0,
                    1.0
                );
        
                // Computing current pixel position in eye space
                vec4 vViewPos = projectionMatrixInverse * vClipPos;
                vViewPos = vViewPos / vViewPos.w;
                vViewPos.z = - vViewPos.z;
        
                // Performing clipping
                #if NUM_CLIPPING_PLANES > 0
                for ( int i = 0; i < NUM_CLIPPING_PLANES; ++ i ) {
                    vec4 plane = clippingPlanes[ i ];
                    if ( dot(vViewPos.xyz, plane.xyz ) > plane.w ) {
                        discard;
                    }
                }
                #endif
        
                // Position in eye coordinates (8)
                vec4 pe = vec4(
                    1.0 / projMatrix11 * (gl_FragCoord.x * 2.0 / w - 1.0),
                    1.0 / projMatrix22 * (gl_FragCoord.y * 2.0 / h - 1.0),
                    -1.0,
                    (f + n)/(2.0 * f * n) -
                        (n - f)/(2.0 * f * n) * (gl_DepthRange.near + gl_DepthRange.far) / gl_DepthRange.diff +
                        z * ((n - f) / (n * f) / gl_DepthRange.diff)
                );
                vec4 np = MTInverse * pe;
                vec4 ne = transpose(MTInverse) * np;
        
                // Declaring normal to get compatibility with threejs Phong chunks
                vec3 normal = normalize(ne.xyz);
        
                // Starting to compute phong lighting
                vec4 diffuseColor = vec4(diffuse, opacity);
                ReflectedLight reflectedLight = ReflectedLight(vec3(0.0), vec3(0.0), vec3(0.0), vec3(0.0));
                vec3 totalEmissiveLight = emissive;
                #include <color_fragment>
                // Declaring specularStength to get compatibility with threejs chunks
                float specularStrength = 1.0;
                #include <lights_phong_fragment>
                #include <lights_fragment_begin>
                vec3 outgoingLight = reflectedLight.directDiffuse + reflectedLight.indirectDiffuse +
                                        reflectedLight.directSpecular + reflectedLight.indirectSpecular + totalEmissiveLight;
        
                gl_FragColor = vec4(outgoingLight, opacity);
        
                //#include <fog_fragment>
            }
        }
        </script>

        <script src="./ts/lib/Lut.js"></script>
        <script src="./ts/lib/justcontext.js"></script>
        <script src="./ts/controls/TransformControls.js"></script>
        <script src="./ts/controls/TrackballControls.js"></script>
        <!-- <script src="./dist/scene/scene_setup.js"></script> -->
        <script src="./dist/scene/instancing.js"></script>
        <script src="./dist/scene/mesh_setup.js"></script>
        <script src="./dist/file_handling/io.js"></script>
        <script src="./dist/scene/PBC_switchbox.js"></script>
        
        <script src="sphere_cylinder_impostor.js"></script>


	</body>
</html>
